CMAKE_MINIMUM_REQUIRED(VERSION 3.10)

PROJECT(VKPT)

IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE "Release" CACHE STRING "" FORCE)
ENDIF()

FIND_PACKAGE(Vulkan REQUIRED)

SET(GLFW_BUILD_EXAMPLES OFF)
SET(GLFW_BUILD_TESTS    OFF)
SET(GLFW_BUILD_DOCS     OFF)
SET(GLFW_INSTALL        OFF)

SET(SHADERC_SKIP_EXAMPLES ON)
SET(SHADERC_SKIP_INSTALL  ON)
SET(SHADERC_SKIP_TESTS    ON)

ADD_SUBDIRECTORY(3rdparty/glfw)
ADD_SUBDIRECTORY(3rdparty/shaderc)
ADD_SUBDIRECTORY(3rdparty/agz-utils)
ADD_SUBDIRECTORY(3rdparty/vk-bootstrap)
ADD_SUBDIRECTORY(3rdparty/vma)

# '3rdparty' folder

MACRO(GET_ALL_TARGETS TARGETS DIR)
    GET_PROPERTY(SUBDIRECTORIES DIRECTORY ${DIR} PROPERTY SUBDIRECTORIES)
    FOREACH(SUBDIR ${SUBDIRECTORIES})
        GET_ALL_TARGETS(${TARGETS} ${SUBDIR})
    ENDFOREACH()
    GET_PROPERTY(CUR_TARGETS DIRECTORY ${DIR} PROPERTY BUILDSYSTEM_TARGETS)
    LIST(APPEND ${TARGETS} ${CUR_TARGETS})
ENDMACRO()

GET_ALL_TARGETS(ALL_TARGETS ${CMAKE_CURRENT_SOURCE_DIR})

FOREACH(T IN ITEMS ${ALL_TARGETS})
	SET_TARGET_PROPERTIES(${T} PROPERTIES FOLDER "3rdparty")
ENDFOREACH()

ADD_SUBDIRECTORY(vkpt)
